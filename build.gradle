buildscript {
	repositories {
		maven { url 'https://plugins.gradle.org/m2/' }
		maven {
			url backstageNexusUrl
			credentials(HttpHeaderCredentials) {
				name = "Deploy-Token"
				value = backstageNexusToken
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}

	dependencies {
		classpath "com.backstage:gradle-plugins:$gradlePluginsVersion"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'backstage-checkstyle'

	group 'com.backstage'
	version "$appVersion"

	repositories {
		mavenCentral()
		maven {
			url backstageNexusUrl
			credentials(HttpHeaderCredentials) {
				name = "Deploy-Token"
				value = backstageNexusToken
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(sourceJavaVersion)
		}
	}

	compileJava.options.encoding = sourceEncoding
	compileTestJava.options.encoding = sourceEncoding

	pluginManager.withPlugin('maven-publish') {
		if (pluginManager.hasPlugin('version-catalog')) {
			return
		}

		if (project.extensions.findByName('PublishingExtension')) {
			return
		}

		javadoc {
			if (JavaVersion.current().isJava9Compatible()) {
				options.addBooleanOption('html5', true)
				options.encoding('UTF-8')
			}
		}

		tasks.register('sourceJar', Jar) {
			archiveClassifier = 'sources'
			from sourceSets.main.allJava
		}

		tasks.register('javadocJar', Jar) {
			archiveClassifier = 'javadoc'
			from javadoc
		}

		publishing {
			publications {
				maven(MavenPublication) {
					groupId = project.group
					artifactId = project.name
					version = project.version

					from components.java
					artifact sourceJar
					artifact javadocJar

					pom {
						licenses {
							license {
								name = "The Apache License, Version 2.0"
								url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
							}
						}
					}
				}
			}

			repositories {
				maven {
					url backstageNexusUrl
					credentials(HttpHeaderCredentials) {
						name = "Deploy-Token"
						value = backstageNexusDeployToken
					}
					authentication {
						header(HttpHeaderAuthentication)
					}
				}
			}
		}
	}
}

wrapper {
	gradleVersion = '8.10'
	distributionType = Wrapper.DistributionType.BIN
}